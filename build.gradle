plugins {
    id 'fabric-loom' version '0.4-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


ext {
    buildNum = (System.getenv("BUILD_NUMBER") != null) ? System.getenv("BUILD_NUMBER") : "0"
}

archivesBaseName = project.archives_base_name
version = project.mod_version + "+build." + buildNum
group = project.maven_group

minecraft {
	refmapName = 'galacticraft-rewoven.refmap.json'
    accessWidener "src/main/resources/galacticraft_rewoven.accesswidener"
}

allprojects {
    repositories {
        mavenLocal()
        jcenter() //Cloth
        maven { url "https://www.curseforge.com/api/maven" }
        maven { url "https://server.bbkr.space/artifactory/libs-release" }
        maven { url "https://maven.modmuss50.me" }
        maven { url "https://mod-buildcraft.com/maven" }
        maven { url "https://dl.bintray.com/ladysnake/libs" }
        maven { url "https://cdn.hrzn.studio/maven" }
    }
}

dependencies {
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings("net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_build}:v2")
    modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

    modCompileOnly fabricApi.module("fabric-api-base", project.fabric_version)
    modCompileOnly fabricApi.module("fabric-biomes-v1", project.fabric_version)
    modCompileOnly fabricApi.module("fabric-blockrenderlayer-v1", project.fabric_version)
    modCompileOnly fabricApi.module("fabric-command-api-v1", project.fabric_version)
    modCompileOnly fabricApi.module("fabric-crash-report-info-v1", project.fabric_version)
    modCompileOnly fabricApi.module("fabric-item-groups-v0", project.fabric_version)
    modCompileOnly fabricApi.module("fabric-networking-blockentity-v0", project.fabric_version)
    modCompileOnly fabricApi.module("fabric-networking-v0", project.fabric_version)
    modCompileOnly fabricApi.module("fabric-object-builder-api-v1", project.fabric_version)
    modCompileOnly fabricApi.module("fabric-particles-v1", project.fabric_version)
    modCompileOnly fabricApi.module("fabric-registry-sync-v0", project.fabric_version)
    modCompileOnly fabricApi.module("fabric-renderer-registries-v1", project.fabric_version)
    modCompileOnly fabricApi.module("fabric-rendering-fluids-v1", project.fabric_version)
    modCompileOnly fabricApi.module("fabric-resource-loader-v0", project.fabric_version)
    modCompileOnly fabricApi.module("fabric-screen-handler-api-v1", project.fabric_version)
    modCompileOnly fabricApi.module("fabric-tag-extensions-v0", project.fabric_version)
    modCompileOnly fabricApi.module("fabric-textures-v0", project.fabric_version)
    modCompileOnly fabricApi.module("fabric-tool-attribute-api-v1", project.fabric_version)

    modImplementation("me.shedaniel.cloth.api:cloth-api:${project.cloth_version}") { transitive = false }
    modImplementation("me.shedaniel.cloth:config-2:${project.cloth_config_version}") {
        exclude group: 'net.fabricmc.fabric-api'
        exclude group: 'net.fabricmc'
    }
    modImplementation("io.github.prospector:modmenu:${project.modmenu_version}") {
        exclude group: 'net.fabricmc.fabric-api'
        exclude group: 'net.fabricmc'
    }
//    modImplementation("me.shedaniel:RoughlyEnoughItems:${project.rei_version}") {
//        exclude group: 'me.shedaniel.cloth'
//        exclude group: 'net.fabricmc.fabric-api'
//        exclude group: 'net.fabricmc'
//        exclude group: 'org.jetbrains'
//    }

    modApi("com.hrznstudio:GalacticraftAPI:${project.addon_version}+1.16.1") { transitive = false }
    modApi("io.github.cottonmc:UniversalComponents:${project.uc_version}") { transitive = false }
    modApi("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cardinal_version}") { transitive = false }
    modApi("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-block:${project.cardinal_version}") { transitive = false }
    modApi("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-item:${project.cardinal_version}") { transitive = false }
    modApi("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cardinal_version}") { transitive = false }
//    modApi("io.github.cottonmc:cotton-resources:${cotton_resources_version}") { transitive = false } //todo add back cotton-resources when it updates

    //include everything mandatory inside the jar
    include("com.hrznstudio:GalacticraftAPI:${project.addon_version}+1.16.1")
    include("me.shedaniel.cloth.api:cloth-api:${project.cloth_version}")
    include("me.shedaniel.cloth:config-2:${project.cloth_config_version}")
    include("io.github.cottonmc:UniversalComponents:${project.uc_version}")
    include("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cardinal_version}")
    include("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-block:${project.cardinal_version}")
    include("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-item:${project.cardinal_version}")
    include("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cardinal_version}")
    include("io.github.cottonmc:cotton-resources:${cotton_resources_version}")

    modRuntime("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}") //Some of our dependencies need other parts of the API (and some just state they depend on the full thing)
    modApi("org.jetbrains:annotations:19.0.0")
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

jar {
    from "LICENSE"
    manifest {
        attributes([
                "Implementation-Title"    : project.archives_base_name,
                "Implementation-Version"  : "${project.version}",
                "Implementation-Vendor"   : "Horizon Studio",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Maven-Artifact"          : "${group}:${archivesBaseName}:${project.version}",
                "ModSide"                 : "BOTH",
        ],)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            groupId = 'com.hrznstudio'
            artifactId = 'galacticraft'

            artifact(jar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
            artifact(javadocJar)
        }
    }
    repositories {
        maven {
            url "s3://cdn.hrzn.studio/maven"
            authentication {
                awsIm(AwsImAuthentication)
            }
        }
    }
}
